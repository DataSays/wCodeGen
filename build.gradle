plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'com.github.ben-manes.versions' version '0.17.0'
}

group = 'org.dataagg'
version = '1.1'
description = """DataAgg wCodeGen"""

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.slf4j:slf4j-log4j12:1.7.25'
	compile 'org.slf4j:jcl-over-slf4j:1.7.25'
	compile 'org.slf4j:jul-to-slf4j:1.7.25'
	compile 'org.bouncycastle:bcprov-jdk16:1.46'
	compile 'org.jodd:jodd-core:3.9.1'
	compile 'org.jodd:jodd-bean:3.9.1'
	compile 'org.jodd:jodd-props:3.9.1'
	compile 'org.jodd:jodd-proxetta:3.9.1'
	compile 'com.google.code.gson:gson:2.8.2'
	compile 'org.freemarker:freemarker:2.3.27-incubating'
	compile 'org.nutz:nutz:1.r.63'
	compile 'org.yaml:snakeyaml:1.19'
	testCompile 'junit:junit:4.12'
}


repositories {
	mavenLocal()
	maven {
		url "http://maven.aliyun.com/nexus/content/groups/public/"
	}
	jcenter()
	//mavenCentral()
}

idea {
	project {
		languageLevel = '1.8'
		ipr {
			withXml {
				provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
			}
		}
	}
	module {
		inheritOutputDirs = false
		outputDir = file("$buildDir/classes/main/")
		testOutputDir = file("$buildDir/classes/test/")
	}
}

jar {
	baseName = 'wCodeGen'
	version = '1.0'

}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.encoding = "UTF-8"
}

task fatJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': 'DataAgg wCodeGen',
				'Implementation-Version': version,
				'Main-Class': 'org.dataagg.codeGen2.FtlCodeGen2'
	}

	baseName = project.name + '-all'
	from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	with jar
}
	
